
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтборПоОрганизации();
	УстановитьПараметрыРедактирования();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоОрганизации()
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ОрганизацияОтбор = Неопределено;
	
	ДополнительныйРеквизитСвойство = Справочники.ММЦ_СсылкиНаОбъекты.НайтиПоНаименованию("Основная организация").СсылкаНаОбъектБазы;
	СтрокаДополнительныйРеквизит = ТекущийПользователь.ДополнительныеРеквизиты.Найти(ДополнительныйРеквизитСвойство, "Свойство");
	Если СтрокаДополнительныйРеквизит <> Неопределено Тогда
		ОрганизацияОтбор = СтрокаДополнительныйРеквизит.Значение;	
	КонецЕсли;
	
	ИдентификаторПользовательскойНастройки = Неопределено;
	Для Каждого Элемент Из Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация") Тогда
			ИдентификаторПользовательскойНастройки = Элемент.ИдентификаторПользовательскойНастройки;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ОрганизацияОтбор) И ЗначениеЗаполнено(ИдентификаторПользовательскойНастройки) Тогда
		Элемент = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ИдентификаторПользовательскойНастройки);
		Элемент.Использование = Истина;
		Элемент.ПравоеЗначение = ОрганизацияОтбор;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыРедактирования()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыВнутреннихДокументов.Ссылка
	|ИЗ
	|	Справочник.ВидыВнутреннихДокументов КАК ВидыВнутреннихДокументов
	|ГДЕ
	|	ВидыВнутреннихДокументов.Родитель = &Родитель
	|	И ВидыВнутреннихДокументов.ЭтоГруппа = &Ложь";
	
	Запрос.УстановитьПараметр("Ложь", Ложь);
	Запрос.УстановитьПараметр("Родитель", Справочники.ММЦ_СсылкиНаОбъекты.НайтиПоНаименованию("СМК (вид документа)").СсылкаНаОбъектБазы);
	
	РезультатЗапроса = Запрос.Выполнить();
	РазрешенныеВидыДокумента = Новый СписокЗначений;
	РазрешенныеВидыДокумента.ЗагрузитьЗначения(РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	
	СхемаКомпоновкиДанных = ОтчетОбъект.СхемаКомпоновкиДанных;
	НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1;
	
	ПолеВидДокумента = НаборДанных.Поля.Найти("ВидДокумента");
	
	ПараметрыВыбора = ПолеВидДокумента.ПараметрыРедактирования.Элементы.Найти("ПараметрыВыбора");	
	ПараметрыВыбора.Значение.Очистить();
	ПараметрыВыбора.Использование = Истина;
	НовыйПараметрВыбора = ПараметрыВыбора.Значение.Добавить();
	НовыйПараметрВыбора.Имя = "Отбор.Ссылка";
	НовыйПараметрВыбора.Значение = РазрешенныеВидыДокумента;
	
	БыстрыйВыбор = ПолеВидДокумента.ПараметрыРедактирования.Элементы.Найти("БыстрыйВыбор");
	БыстрыйВыбор.Значение = Истина;
	БыстрыйВыбор.Использование = Истина;
	
	//ФормаВыбора = ПолеВидДокумента.ПараметрыРедактирования.Элементы.Найти("ФормаВыбора");	
	//ФормаВыбора.Значение = "Справочник.ВидыВнутреннихДокументов.Форма.ФормаВыбора";
	//ФормаВыбора.Использование = Истина;
	//ОтображениеТаблицы.Список
	
	АдресСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
	ПользовательскиеНастройки = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	
	Отчет.КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
	Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	Отчет.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ПользовательскиеНастройки);
	
КонецПроцедуры




